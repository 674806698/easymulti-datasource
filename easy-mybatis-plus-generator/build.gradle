plugins {
    id 'java'
    // maven 发布插件
    id 'maven-publish'
    // 签名插件
    id 'signing'
}

group 'com.github.wujiuye'
// 与mybatis-plus-generator版本保持一致
version '3.3.2'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-logging:2.3.0.RELEASE'
    compile group: 'com.baomidou', name: 'mybatis-plus-generator', version: '3.3.2'
    compile 'org.apache.velocity:velocity:1.7'
    compile 'org.apache.velocity:velocity-engine-core:2.2'
    compile 'org.freemarker:freemarker:2.3.30'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'mysql:mysql-connector-java:5.1.29'
}

compileJava.dependsOn(processResources)

// 打包源码
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

// 生成 javadoc jar
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        // 推送
        publishEasyMybatisPlusGeneratorProject(MavenPublication) {
            // 编译jar包
            from components.java
            // 打包源码
            artifact sourcesJar
            // javadoc
            artifact javadocJar

            pom {
                name = "easy-mybatis-plus-generator"
                description = "mybatis-plus代码生成器封装，只需配置文件简单配置即可使用，并去掉了默认的Service、ServiceImpl、Controller的生成"
                url = "https://github.com/wujiuye/easymulti-datasource-spring-boot-starter"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "wujiuye"
                        name = "wujiuye"
                        email = "419611821@qq.com"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/wujiuye/easymulti-datasource-spring-boot-starter.git"
                    developerConnection = "scm:git:https://github.com/wujiuye/easymulti-datasource-spring-boot-starter.git"
                    url = "https://github.com/wujiuye/easymulti-datasource-spring-boot-starter"
                }
            }
        }
    }
    // 到 https://oss.sonatype.org/#nexus-search; 搜索是否上传成功
    repositories {
        // 发布Release版本可在版本号后面带上'-RELEASE'，也可不带
        maven {
            name 'Release'
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            credentials {
                username = "${NEXUS_USERNAME}"
                password = "${NEXUS_PASSWORD}"
            }
        }
        // 发布Snapshot快照版本必须在版本号后面加上'-SNAPSHOT'
        maven {
            name = 'Snapshot'
            url = 'https://oss.sonatype.org/content/repositories/snapshots'
            credentials {
                username = "${NEXUS_USERNAME}"
                password = "${NEXUS_PASSWORD}"
            }
        }
    }
}

// 必须在 publishing 配置之后
signing {
    sign publishing.publications.publishEasyMybatisPlusGeneratorProject
}


